# Best practice

> This file shall have the **best** snippets of classes or functions that we usually reuse in other code.  As well the names patterns



# Writing pattern

### Universal patterns

> **Setting variables** 
> They should always be preceded by **sys_**
> Ex.: When setting vba ranges such as table headers with a cell. Default cell name should be cnCellName, but as it set something elsewhere it should be at Setting sheet and named as sys_cnCellName
> \-- sys = system

> **Data relationship**
> They should always be preceded by FK_[relationship type]
> Ex.: When creating a database and want to relate the IDs, all foreigner keys should be preceded by FK_ID
> \-- FK = foreigner key

> **Auxiliary elements** 
> They should always be preceded by **aux_**. 
> Ex.: When manipulating tables through VBA that are used for Excel equations: **Aux_tbPRETableName**
> 
> \-- Aux = Auxiliary).

## Excel related prefix

### VBA

> **Coding structure**
> \-- The worksheets should have nothing else other then evaluation and triggers, all main code should be at the **"WorksheetFunctions"** module
> \-- All error handlers should be at **"Error handler"** module
> 
> \-- Any code that is called should be at **"CrossModules"** Mudule

* **Sheet name at VBA:**
  
  * vb[Sheet name]
    
    * vb = Visual Basic
  
  * Ex.: 

* **Parameters and Arguments:**
  
  * \*[Parameter value]
    
    * bv = ByValue: arguments which are not linked to the calling function (so the value may be changed on the local code without changing the value on the request function)
      
      * bf = ByRef: arguments which are linked to the calling function (so the value changed on the local code will also change the value on the request function)
    
    * mcl = Modula Class: Variables set as private on class
    
    * cl = Class: Variables that can be seen or /and written when calling the object 

* **Functions at VBA:**
  
  * vf[Function name - First letters always in CAPS]
    
    * vf = Vba Functions
  
  * Ex.:

* **Sub at VBA:**
  
  * vs[Sub name]
    
    * vs = Vba Sub
  
  * Ex.:

* **Variables at VBA:**
  
  * \*[Variable Name]
    
    * d = Dim: Normal variables such as integer ad string
    
    * s = Set: Variables that can't simply receive a value but need to be set such as FileDialog and ranges
    
    * rs = RecordSet: Variables of type ADODB.recordset
    
    * wb = wADODB: Variables of type ADODB.connection

### M / dax

* **Query table names:**
  
  * qr_[query name, can be shortened as one likes - First letters always in caps]
    
    * qr = Query
  
  * Ex.:

* **Query Functions:**
  
  * fn[Query name - First letters always in CAPS]
    
    * fn = Function
  
  * Ex.:

### Excel

* **Table names:**
  
  * tb[First letters of sheet's name - All in CAPS][name of the table, can be shortened as one likes - First letters always in CAPS]
    
    * tb = Table
  
  * Ex.:

* **'LET' static variables:** 
  
  * v[Variable name - First letters always in CAPS]
    
    * v = Variable
  
  * Ex.:

* **Named cells:**
  
  * cn[Variable name - First letters always in CAPS]
    * cn = Cell Name
  * Ex.:

* **Lambda functions:**
  
  * \*[Variable name - First letters always in CAPS]
    
    * lmd = Lambda:  Prefix for the variable that is calling lambda (name can be shortened for default formulas)
    * lm = lm: Internal Prefix for the internal variable of lambda
  
  * Ex.: Let(lmdName,LAMBDA(lmOtherName,...))

* **Object names:**
  
  * obj[First letters of sheet's name - All in CAPS][Object name - First letters always in CAPS]
    
    * obj = Object

# Best Snippets

## VBA

```visual-basic
'
' This resizes a table to a one row size... Doesn't matter where it is AT.
'

' Usage. Change the table name from VBA (FindOneCode) and on sheet (TbProtocolToInsert)
FindOneCode.ListObjects("TbProtocolToInsert").Resize Intersect(Range("TbProtocolToInsert[#all]"), Range(Range("TbProtocolToInsert").Row & ":" & Range("TbProtocolToInsert").Row - 1))
```



```visual-basic
Function vfHexToRGB(HexColor As String) As Long
'
' This will convert the Hex codes to RGB, Easy picking on color selector with hex
'

'
    Dim r As Long, g As Long, b As Long

    ' Remove the "#" if it's there
    If Left(HexColor, 1) = "#" Then HexColor = Mid(HexColor, 2)

    ' Convert the hex strings to RGB values
    r = Val("&H" & Mid(HexColor, 1, 2))
    g = Val("&H" & Mid(HexColor, 3, 2))
    b = Val("&H" & Mid(HexColor, 5, 2))

    vfHexToRGB = RGB(r, g, b)
End Function
```



## Excel

### Lambda

```excel-formula
/* Return true if the value is not empty or 0
*/

name: lmdNotEmptyOrZero -> lmdNEZ
=LAMBDA(Cell,IF(AND(Cell<>0,Cell<>""),TRUE,FALSE))
```

### Normal

```excel-formula
/* ERPM DashBoard (DB)    
*/

=LET(
  REM,"This returns TRUE if target has only 0 to 9 and A to Z (upper or lowercase) characters. You may set to allow or not empty strings and Spaces",
  vTarget,"As string or range",
  vAllowEmptyTarget,FALSE,
  vAllowSpace,TRUE,

  lmdConRemoveSpace,LAMBDA(lmTxt,IF(vAllowSpace,SUBSTITUTE(lmTxt," ",""),lmTxt)),
  vTargetArray, CODE(MID(lmdConRemoveSpace(vTarget), SEQUENCE(LEN(lmdConRemoveSpace(vTarget))), 1)),
  IF(vTarget<>"",
    IF(SUMPRODUCT(--( (vTargetArray>=65)*(vTargetArray<=90) + (vTargetArray>=97)*(vTargetArray<=122) + (vTargetArray>=48)*(vTargetArray<=57) ))=LEN(lmdConRemoveSpace(vTarget)),
      TRUE,FALSE
    ),
    vAllowEmptyTarget
  )
)

=LET(
  REM,"This returns TRUE if the character is at ASCII",
  vTarget,"As string or range",
  vAllowEmptyTarget,FALSE,
  vTargetArray, CODE(MID(vTarget, SEQUENCE(LEN(vTarget)), 1)),

  IF(vTarget<>"",
    IF(max(vTargetArray)<=127,
      TRUE,FALSE
    ),
    vAllowEmptyTarget
  )
)
```



```excel-formula
/* Calculadora de precificação sacos para lixo

Based on the given conditions, the formula looks up a value at the chosen index. If no value is found, it searches at lower indices. For example, if the index is set to 4 (referring to column 4) and no value exists, it retrieves the value from index 3. There are no restrictions on conditions or the CHOOSE function, adapt it as needed.

Note: If the lookup fails at the chosen index, the result will be returned as a negative number. Use ABS to correct it or format the cell to highlight negative values and hide the (-). Also, the conditions should always yield a single result—if multiple matches are found, the formula will return an error message (you may treat that, to yiled multiply results).

*/

=LET(
  vConditions,
         ($G8 = tbPCCalculator[Litragem]) *
         (IF($B$5, $C$5, $D$5) = tbPCCalculator[Espessura]) *
         (tbPCCalculator[Und] = 100) *
         ($B$3 = tbPCCalculator[Material]) *
         (tbPCCalculator[Status] = "Ativo"),
  vCountValidConditions, SUMPRODUCT(vConditions),
  vOrigIndex, $B$2 + 1 + N("Set indext to start at 1 as ours starts at 0"),
  vFallBackIndices, IF(vOrigIndex>=2, SEQUENCE(vOrigIndex-1, 1, vOrigIndex, -1),IF(vOrigIndex=1,{1},{0})),
  vCandidateArray,
    MAP(vFallBackIndices,
      LAMBDA(lmIndex,
        IF(lmIndex=0, 0,
           SUMPRODUCT(vConditions, CHOOSE(lmIndex,
             tbPCCalculator[Preço de venda Recomendado],
             tbPCCalculator[PV Client Final],
             tbPCCalculator[PV Sem compromisso],
             tbPCCalculator[PV Venda em volume],
             tbPCCalculator[PV Abatendo concorrentes]
           ))
        )
      )
    ),
  vFirstCandidateEmpty,IF(INDEX(vCandidateArray,1)=0,TRUE,FALSE),
  vValidCandidates, FILTER(vCandidateArray, vCandidateArray<>0,0),
  vResultCandidate, IF(AND(ROWS(vValidCandidates)=1,INDEX(vValidCandidates,1)=0), 0, INDEX(vValidCandidates, 1)),
  IF(vCountValidConditions >= 2, "Err. Unexpected duplicate",
    IF(vCountValidConditions = 0, "",
      IF(vFirstCandidateEmpty,vResultCandidate*-1,vResultCandidate
      )
    )
  )
)
```



```excel-formula
/* Calculadora de precificação sacos para lixo

Xlookup a value crossing the calling column and a header option - alternative to use one single datasheet for user fast use.

Needs a integer sup_row and sup_column based on the first column (or the calling column):
    =ROW([@Litragem])-MIN(ROW([Litragem]))+2 (result should represent the rows sequence inside the table)
    =COLUMN()-COLUMN(tbSDimentionBags[[#Headers],[Litragem]])+1 (result should represent the columns sequence inside the table)
*/

=INDEX(
  tbSDimentionBags[#All],
  XLOOKUP([@Litragem],tbSDimentionBags[Litragem],tbSDimentionBags[Sup_Row]),
  XLOOKUP([@Material],tbSDimentionBags[#Headers],tbSDimentionBags[#Totals])
)
```



## M

```powerquery
/* temp file loading
*/
let
    NormalizeSpaces = (bvInputTable as table, bvColumnName as text) as table => let
        normalizedTable = Table.TransformColumns(
            bvInputTable,
            {
                {bvColumnName,
                    each Text.Combine(
                        List.Select(
                            Text.Split(Text.Trim(_), " "), each _ <> ""
                        ), " "
                    ), type text
                }
            }
        )
    in
        normalizedTable
in
    NormalizeSpaces
```



## Dax

# 

